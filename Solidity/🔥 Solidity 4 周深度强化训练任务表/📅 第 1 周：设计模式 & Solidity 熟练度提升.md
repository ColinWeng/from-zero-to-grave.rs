## **D1 â€” Solidity åŸºç¡€å¤ç›˜ + é“¶è¡Œåˆçº¦**

- **å­¦ä¹ ç‚¹**ï¼š
    - çŠ¶æ€å˜é‡ï¼ˆ`storage`/`memory`/`calldata`ï¼‰
    - `public/private/internal/external`Â å¯è§æ€§
    - `require`Â /Â `revert`Â /Â `assert`
- **å®æ“ä»»åŠ¡**ï¼š
  
    - å†™ä¸€ä¸ªÂ `Bank.sol`Â åˆçº¦ï¼š
        - å­˜æ¬¾ï¼ˆ`deposit()`ï¼‰
        - æŸ¥è¯¢ä½™é¢ï¼ˆ`getBalance()`ï¼‰
        - ææ¬¾ï¼ˆ`withdraw(uint amount)`ï¼‰
- **æµ‹è¯•**ï¼š
  
    - ç”¨ Hardhat å†™ JS/TS å•å…ƒæµ‹è¯•ï¼ˆchai + ethers.jsï¼‰
      
        ```bash
        npx hardhat test
        ```
    
- **è¾“å‡º**ï¼š
  
    - åˆçº¦ä»£ç  + æµ‹è¯•è¦†ç›–ç‡æˆªå›¾

--------

### ç¤ºä¾‹ä»£ç 

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

/*
 * å†™ä¸€ä¸ª `Bank.sol` åˆçº¦ï¼š
    - å­˜æ¬¾ï¼ˆ`deposit()`ï¼‰
    - æŸ¥è¯¢ä½™é¢ï¼ˆ`getBalance()`ï¼‰
    - ææ¬¾ï¼ˆ`withdraw(uint amount)`ï¼‰
 */
contract Bank {

    // å­˜å‚¨ç”¨æˆ·çš„ä½™é¢
    mapping(address => uint256) private balances;

    event DepositEvent(address indexed user, uint256 amount);
    event WithdrawEvent(address indexed user, uint256 amount);

    // å­˜æ¬¾ï¼ˆ`deposit()`ï¼‰
    // è¦è®©ç”¨æˆ·è°ƒç”¨ï¼Œå¹¶èƒ½æ”¯ä»˜ï¼Œæ‰€ä»¥éœ€è¦ external å’Œ payable
    // å…¨å±€å˜é‡ `msg.value` è¡¨ç¤º æœ¬æ¬¡è°ƒç”¨å‘é€çš„ Wei æ•°é‡
    function deposit() external payable {
        require(msg.value > 0, "deposit value must > 0");
        balances[msg.sender] += msg.value;
        // å‘é€å­˜æ¬¾äº‹ä»¶
        emit DepositEvent(msg.sender, msg.value);
    }

    // æŸ¥è¯¢ä½™é¢ï¼ˆ`getBalance()`ï¼‰
    // æŸ¥è¯¢è‡ªå·±çš„ä½™é¢ï¼Œä¸ä¼šè¿›è¡Œä¿®æ”¹ï¼Œæ‰€ä»¥éœ€è¦ external å’Œ view
    // å…¨å±€å˜é‡ `msg.sender` è¡¨ç¤º å½“å‰è°ƒç”¨è€…åœ°å€
    function getBalance() external view returns (uint256) {
        return balances[msg.sender];
    }

    // ææ¬¾ï¼ˆ`withdraw(uint amount)`ï¼‰
    function withdraw(uint amount) external {
        require(amount > 0, "withdraw amount value must > 0");
        require(balances[msg.sender] >= amount, "Insufficient balance");
        // å…ˆå‡ï¼Œé˜²æ­¢é‡å…¥
        balances[msg.sender] -= amount;
        // å‘èµ·è½¬è´¦
        (bool sent,) = payable(msg.sender).call{value: amount}("");
        // æ ¡éªŒè½¬è´¦ç»“æœ
        require(sent, "call failed");
        //  å‘é€è½¬è´¦äº‹ä»¶
        emit WithdrawEvent(msg.sender, amount);
    }
}
```

###  æµ‹è¯•ä»£ç 

åˆ›å»º `Bank.t.sol` æ–‡ä»¶

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

import {Test} from "forge-std/Test.sol";
import {Bank} from "./Bank.sol";

contract BankTest is Test {

    Bank bank;

    address user1;
    address user2;

    function setUp() public {
        // ç»™æµ‹è¯•åœ°å€ç”Ÿæˆ
        user1 = makeAddr("user1");
        user2 = makeAddr("user2");

        // éƒ¨ç½²åˆçº¦
        bank = new Bank();

        // ç»™ user1 & user2 åˆå§‹ ETH ä½™é¢
        vm.deal(user1, 100 ether);
        vm.deal(user2, 100 ether);
    }

    // æµ‹è¯•å­˜æ¬¾
    function testDeposit() public {
        uint256 depositAmount = 1 ether;

        // ä»¥ user1 èº«ä»½è°ƒç”¨ deposit
        vm.prank(user1);
        bank.deposit{value: depositAmount}();

        uint256 balance = bank.getBalance();
        // æ³¨æ„ï¼šè°ƒç”¨ getBalance() æ—¶ï¼Œé»˜è®¤ msg.sender æ˜¯æœ¬åˆçº¦ï¼ˆTestï¼‰ï¼Œè€Œä¸æ˜¯ user1
        // æ‰€ä»¥æˆ‘ä»¬ç”¨ vm.prank æ¥æŒ‡å®š msg.sender
        vm.prank(user1);
        uint256 userBalance = bank.getBalance();

        assertEq(userBalance, depositAmount);
    }

    // æµ‹è¯•ææ¬¾æˆåŠŸ
    function testWithdraw() public {
        uint256 depositAmount = 1 ether;

        // user1 å­˜æ¬¾
        vm.prank(user1);
        bank.deposit{value: depositAmount}();

        // user1 ææ¬¾
        vm.startPrank(user1);
        bank.withdraw(depositAmount);
        vm.stopPrank();

        // éªŒè¯ä½™é¢åº”ä¸º 0
        vm.prank(user1);
        uint256 finalBalance = bank.getBalance();
        assertEq(finalBalance, 0);
    }

    // æµ‹è¯•ææ¬¾å¤±è´¥ï¼ˆä½™é¢ä¸è¶³ï¼‰
    function testWithdrawRevertWhenInsufficientBalance() public {
        vm.prank(user1);
        vm.expectRevert(bytes("Insufficient balance"));
        bank.withdraw(1 ether);
    }

    // æµ‹è¯•äº‹ä»¶ â€” å­˜æ¬¾
    function testDepositEvent() public {
        vm.expectEmit(true, true, true, true);
        emit Bank.Deposit(user1, 1 ether);

        vm.prank(user1);
        bank.deposit{value: 1 ether}();
    }

    // æµ‹è¯•äº‹ä»¶ â€” ææ¬¾
    function testWithdrawEvent() public {
        vm.prank(user1);
        bank.deposit{value: 1 ether}();

        vm.expectEmit(true, true, true, true);
        emit Bank.Withdraw(user1, 1 ether);

        vm.prank(user1);
        bank.withdraw(1 ether);
    }
}
```

bash æ‰§è¡Œ `npx hardhat test solidity`ã€‚

> ä½¿ç”¨  IntelliJ IDEA å¼€å‘ï¼Œç¯å¢ƒé…ç½®å¥½ï¼Œå¯ä»¥ç›´æ¥è¿è¡Œã€‚



### TypeScript æµ‹è¯•

test ç›®å½•ä¸‹åˆ›å»º `Bank.ts` ã€‚

```ts
import {expect} from "chai";
import {network} from "hardhat";

const {ethers} = await network.connect();

describe("Bank Contract (TypeScript)", function () {

    it("should allow deposit and update balance correctly", async function () {
        // è·å–æµ‹è¯•è´¦æˆ·
        const [user1, user2] = await ethers.getSigners();
        const bank = await ethers.deployContract("Bank");
        await bank.waitForDeployment();

        const depositAmount = ethers.parseEther("1.0");

        await expect(
            bank.connect(user1).deposit({value: depositAmount})
        )
            .to.emit(bank, "Deposit")
            .withArgs(user1.address, depositAmount);

        const balance = await bank.connect(user1).getBalance();
        expect(balance).to.equal(depositAmount);
    });

});
```

æ¯”è¾ƒä¸‹ï¼Œæ›´å€¾å‘äº Solidity æµ‹è¯• çš„æ–¹å¼ã€‚



---

## **D2 â€” Ownable æƒé™æ§åˆ¶**

- **å­¦ä¹ ç‚¹**ï¼š
    - OpenZeppelinÂ `Ownable`
    - `modifier`Â ç”¨æ³•
- **å®æ“ä»»åŠ¡**ï¼š
    - åœ¨Â `Bank.sol`Â ä¸­å¢åŠ åªæœ‰Â `owner`Â èƒ½è°ƒç”¨çš„Â `closeBank()`Â å‡½æ•°
- **æµ‹è¯•**ï¼š
    - ç”¨ FoundryÂ `.t.sol`Â å†™æƒé™æµ‹è¯•
      
        ```bash
        forge test
        ```
    
- **è¾“å‡º**ï¼šFoundry æµ‹è¯•é€šè¿‡æˆªå›¾

--------

### ç¤ºä¾‹ä»£ç 

å®‰è£… OpenZeppelin ä¾èµ–

```bash
npm install @openzeppelin/contracts
forge install OpenZeppelin/openzeppelin-contracts   
```



```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

// å¼•å…¥ OpenZeppelin Ownable
// å¦‚æœæœ‰  '"@openzeppelin/contracts/access/Ownable.sol"' cannot be resolved çš„æç¤º ï¼Œ
// æ‰§è¡Œ 
//     npm install @openzeppelin/contracts
//     å¦‚æœå‡ºç°ï¼ˆfatal: not a git repository (or any of the parent directoriesï¼‰ 
//     æ‰§è¡Œ  git init
//     forge install OpenZeppelin/openzeppelin-contracts   
import "@openzeppelin/contracts/access/Ownable.sol";

/*
    åœ¨`Bank.sol`ä¸­å¢åŠ åªæœ‰`owner`èƒ½è°ƒç”¨çš„`closeBank()`å‡½æ•°
 */
// ç»§æ‰¿ æŠ½è±¡åˆçº¦ Ownable
contract Bank is Ownable {

    mapping(address => uint256) private balances;

    event Deposit(address indexed user, uint256 amount);
    event Withdraw(address indexed user, uint256 amount);
    // æ–°å¢å…³é—­äº‹ä»¶
    event BankClosed(address indexed owner, uint256 remainingBalance);

    /*
     * åœ¨ Solidity ä¸­ï¼Œåˆçº¦çš„æ„é€ å‡½æ•°æ‰§è¡Œé¡ºåºæ˜¯ï¼š
        çˆ¶åˆçº¦çš„æ„é€ å‡½æ•° ä¼šåœ¨å­åˆçº¦çš„æ„é€ å‡½æ•°ä½“æ‰§è¡Œ ä¹‹å‰ è°ƒç”¨ã€‚
        æ‰§è¡Œé¡ºåºæŒ‰ç…§ç»§æ‰¿å…³ç³»ï¼Œä»æœ€é¡¶å±‚çš„çˆ¶åˆçº¦å‘ä¸‹ã€‚
        å¦‚æœçˆ¶åˆçº¦æœ‰æ„é€ å‡½æ•°å‚æ•°ï¼Œå¯ä»¥åœ¨å­åˆçº¦æ„é€ å‡½æ•°çš„ç­¾ååä¸€å¯¹æ‹¬å·é‡Œä¼ å…¥ï¼ˆåƒä½ å†™çš„ Ownable(msg.sender)ï¼‰ã€‚
     */
    constructor() Ownable(msg.sender){
        // é»˜è®¤ owner æ˜¯éƒ¨ç½²è€…
    }

    function deposit() external payable {
        require(msg.value > 0, "deposit value must > 0");
        balances[msg.sender] += msg.value;
        emit Deposit(msg.sender, msg.value);
    }

    function getBalance() external view returns (uint256) {
        return balances[msg.sender];
    }

    function withdraw(uint amount) external {
        require(amount > 0, "withdraw amount value must > 0");
        require(balances[msg.sender] >= amount, "Insufficient balance");
        balances[msg.sender] -= amount;
        (bool sent,) = payable(msg.sender).call{value: amount}("");
        require(sent, "call failed");
        emit Withdraw(msg.sender, amount);
    }

    // åªæœ‰ owner å¯ä»¥å…³é—­é“¶è¡Œ
    function closeBank() external onlyOwner {
        // mapping(address => uint256) private balances è®°å½•ç”¨æˆ·ä½™é¢ï¼Œ
        // address(this).balance ç›´æ¥è¯»å– EVM åˆçº¦ä¸Šçš„ balanceï¼Œ
        // å½“å‰åˆçº¦åœ°å€çš„ä½™é¢
        uint256 remaining = address(this).balance;
        // è½¬è´¦æ‰€æœ‰èµ„é‡‘ç»™ owner
        if (remaining > 0) {
            (bool sent,) = payable(owner()).call{value: remaining}("");
            require(sent, "Transfer failed");
        }
        emit BankClosed(owner(), remaining);
    }
}
```

###  æµ‹è¯•ä»£ç 

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

import {Test} from "forge-std/Test.sol";
import {Bank} from "./Bank.sol";

contract BankTest is Test {

    Bank public bank;
    address owner;
    address user1;
    address user2;

    function setUp() public {
        owner = makeAddr("owner");
        user1 = makeAddr("user1");
        user2 = makeAddr("user2");

        vm.deal(owner, 100 ether);
        vm.deal(user1, 100 ether);
        vm.deal(user2, 100 ether);

        vm.startPrank(owner);
        bank = new Bank();
        vm.stopPrank();
    }

    function testBalancesMatchContractBalance() public {
        // ä»¥ user1 èº«ä»½è°ƒç”¨ deposit
        vm.prank(user1);
        bank.deposit{value: 5 ether}();

        vm.prank(user1);
        uint256 user1Balance = bank.getBalance();

        // ä»¥ user2 èº«ä»½è°ƒç”¨ deposit
        vm.prank(user2);
        bank.deposit{value: 3 ether}();

        vm.prank(user2);
        uint256 user2Balance = bank.getBalance();

        // è·å–é€»è¾‘ä½™é¢æ€»å’Œ
        uint256 totalLogicBalance = user1Balance + user2Balance;
        // è·å–çœŸå®é“¾ä¸Šä½™é¢
        uint256 contractBalance = address(bank).balance;

        assertEq(totalLogicBalance, contractBalance, "The total logical balance should be equal to the real balance of the contract");
    }

    function testCloseBankTransfersAllToOwner() public {
        // ç”¨æˆ·1å­˜ 5 ETH
        vm.prank(user1);
        bank.deposit{value: 5 ether}();

        uint256 ownerBefore = owner.balance;

        // ğŸ¯ å£°æ˜æˆ‘ä»¬æœŸæœ›æ•è·çš„äº‹ä»¶
        vm.expectEmit(true, false, false, true);
        emit Bank.BankClosed(owner, 5 ether); // æ³¨æ„è¿™é‡Œè¦åŠ  Bank. å‰ç¼€è°ƒç”¨äº‹ä»¶ç­¾å

        // owner å…³é—­é“¶è¡Œ
        vm.startPrank(owner);
        bank.closeBank();
        vm.stopPrank();

        // éªŒè¯åˆçº¦ä½™é¢ä¸º 0
        assertEq(address(bank).balance, 0, "The contract balance should be 0");

        // éªŒè¯ owner æ”¶åˆ°èµ„é‡‘
        assertEq(owner.balance, ownerBefore + 5 ether, "owner All funds should be received");
    }
}
```



---

## **D3 â€” Withdraw Patternï¼ˆæ‹‰å–æ”¯ä»˜ï¼‰**

- **å­¦ä¹ ç‚¹**ï¼š
    - é‡å…¥é£é™©ä¸è§£å†³
    - å­˜å‚¨ç”¨æˆ·ä½™é¢ï¼Œ**æç°æ—¶ç”¨æˆ·è‡ªå·±è°ƒç”¨**
- **å®æ“ä»»åŠ¡**ï¼š
    - æ–°å»ºÂ `PullPaymentBank.sol`
    - å­˜æ¬¾æ—¶å¢åŠ ä½™é¢è®°å½•
    - æç°æ—¶è¯»å–ä½™é¢ â†’ è½¬è´¦ â†’ ä½™é¢æ¸…é›¶
- **æµ‹è¯•**ï¼š
    - æµ‹è¯•å¤šè´¦æˆ·äº’ä¸å½±å“ä½™é¢
- **è¾“å‡º**ï¼šHardhat & Foundry éƒ½æµ‹è¯•é€šè¿‡

---

### ç¤ºä¾‹ä»£ç 

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.10;

import "@openzeppelin/contracts/utils/ReentrancyGuard.sol";

/*
- æ–°å»º`PullPaymentBank.sol`
- å­˜æ¬¾æ—¶å¢åŠ ä½™é¢è®°å½•
- æç°æ—¶è¯»å–ä½™é¢ â†’ è½¬è´¦ â†’ ä½™é¢æ¸…é›¶
 */
// ä½¿ç”¨ openzeppelin æä¾›çš„é˜²é‡å…¥æŠ½è±¡ç±»
contract PullPaymentBank is ReentrancyGuard{

    mapping(address => uint256) private balances;

    event Deposited(address indexed user, uint256 amount);
    event Withdrawn(address indexed user, uint256 amount);

    // å­˜æ¬¾
    function deposit() external payable {
        require(msg.value > 0, "Deposit must be greater than 0");
        balances[msg.sender] += msg.value;
        emit Deposited(msg.sender, msg.value);
    }

    // æŸ¥çœ‹ä½™é¢
    function getBalance() external view returns (uint256) {
        return balances[msg.sender];
    }

    // æç° â€” æ‹‰å–æ”¯ä»˜æ¨¡å¼
    // ä½¿ç”¨ å‡½æ•°ä¿®æ”¹å™¨ï¼ˆnonReentrantï¼‰ï¼Œé˜²é‡å…¥
    function withdraw() external nonReentrant {
        uint256 amount = balances[msg.sender];
        require(amount > 0, "No balance to withdraw");

        // å…ˆå°†ä½™é¢æ¸…é›¶
        balances[msg.sender] = 0;

        // å†è½¬è´¦
        (bool sent, ) = payable(msg.sender).call{value: amount}("");
        require(sent, "Transfer failed");

        emit Withdrawn(msg.sender, amount);
    }
}
```

**å…³é”®ç‚¹**ï¼š

1. **çŠ¶æ€æ›´æ–°åœ¨å…ˆï¼ˆbalances ç½®ä¸º 0ï¼‰**ï¼Œå¤–éƒ¨è°ƒç”¨åœ¨å â†’ é˜²æ­¢é‡å…¥
2. ä½¿ç”¨ `ReentrancyGuard` åŒä¿é™©
3. `withdraw()` æ—¶ä¸ä¼ å‚æ•°ï¼Œåªèƒ½æå–è‡ªå·±ä½™é¢ â†’ é¿å…è¶Šæƒ

### æµ‹è¯•ä»£ç 

`PullPaymentBank.t.sol` 

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

import {Test} from "forge-std/Test.sol";
import {PullPaymentBank} from "./PullPaymentBank.sol";

contract PullPaymentBankTest is Test {
    PullPaymentBank public bank;
    address user1;
    address user2;

    function setUp() public {
        bank = new PullPaymentBank();
        user1 = makeAddr("user1");
        user2 = makeAddr("user2");

        vm.deal(user1, 100 ether);
        vm.deal(user2, 100 ether);
    }


    function testMultipleAccountsIndependentBalances() public {
        // user1 å­˜ 5 ETH
        vm.prank(user1);
        bank.deposit{value: 5 ether}();

        // user2 å­˜ 3 ETH
        vm.prank(user2);
        bank.deposit{value: 3 ether}();

        // æ£€æŸ¥å„è‡ªä½™é¢
        vm.prank(user1);
        assertEq(bank.getBalance(), 5 ether);

        vm.prank(user2);
        assertEq(bank.getBalance(), 3 ether);

        // user1 æç°
        vm.prank(user1);
        bank.withdraw();
        assertEq(user1.balance, 100 ether); // æå›æ¥äº†

        // user2 ä¸å—å½±å“
        vm.prank(user2);
        assertEq(bank.getBalance(), 3 ether);
    }

}

```

æ”»å‡»åˆçº¦ `ReentrancyAttackPull`

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.10;

import "./PullPaymentBank.sol";

/**
 * æ¶æ„æ”»å‡»åˆçº¦ï¼Œè¿™ä¸ªæ”»å‡»åˆçº¦ä¼šåœ¨æ”¶åˆ°é’±æ—¶ï¼Œå°è¯•å†æ¬¡è°ƒç”¨ withdraw() æ¥é‡å…¥
 */
contract ReentrancyAttackPull {

    PullPaymentBank public bank;
    address public owner;

    constructor(address _bank) {
        bank = PullPaymentBank(_bank);
        owner = msg.sender;
    }

    // åœ¨æ”¶åˆ° ETH æ—¶å°è¯•å†è°ƒç”¨ withdraw()
    receive() external payable {
        if (address(bank).balance >= 1 ether) {
            bank.withdraw(); // åœ¨å®‰å…¨åˆçº¦é‡Œä¼šå¤±è´¥
        }
    }

    // å¯åŠ¨æ”»å‡»æµç¨‹
    function attack() external payable {
        require(msg.value >= 1 ether, "Need >= 1 ETH");

        // é¦–å…ˆå­˜å…¥ 1 ETH
        bank.deposit{value: msg.value}();

        // å‘èµ·ç¬¬ä¸€æ¬¡æç°
        bank.withdraw();
    }

    // æå–æœ¬åˆçº¦ä¸­èµ„é‡‘
    function withdrawStolenFunds() external {
        require(msg.sender == owner, "Only owner");
        payable(owner).transfer(address(this).balance);
    }
}

```

æµ‹è¯•é‡å…¥æ”»å‡»  `ReentrancyTestPull.t.sol`

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import "./ReentrancyAttackPull.sol";
import {Test} from "forge-std/Test.sol";

contract ReentrancyTestPull is Test {

    PullPaymentBank public bank;
    ReentrancyAttackPull public attacker;

    address deployer = makeAddr("deployer");
    address user1 = makeAddr("user1");

    function setUp() public {
        vm.startPrank(deployer);
        bank = new PullPaymentBank();
        vm.stopPrank();

        // ç»™ user1 å’Œæ”»å‡»åˆçº¦ä¸€äº›å¯åŠ¨èµ„é‡‘
        vm.deal(user1, 10 ether);

        // éƒ¨ç½²æ”»å‡»åˆçº¦
        vm.startPrank(user1);
        attacker = new ReentrancyAttackPull(address(bank));
        vm.stopPrank();
    }

    function testReentrancyFails() public {
        emit log_named_uint("Bank balance before", address(bank).balance);
        emit log_named_uint("Attacker balance before", address(attacker).balance);

        // user1 å‘åŠ¨æ”»å‡»
        vm.startPrank(user1);
        attacker.attack{value: 1 ether}();
        vm.stopPrank();

        emit log_named_uint("Bank balance after", address(bank).balance);
        emit log_named_uint("Attacker balance after", address(attacker).balance);

        // éªŒè¯é“¶è¡Œè¿˜å‰©é’±ï¼ˆæ”»å‡»æœªé€ æˆæŸå¤±ï¼‰
        assertEq(address(bank).balance, 0, "Bank should remain empty after user's withdrawal");
        // éªŒè¯æ”»å‡»è€…åªæ‹¿å›äº†è‡ªå·±çš„é’±ï¼ˆæ²¡å·é¢å¤–èµ„é‡‘ï¼‰
        assertEq(address(attacker).balance, 1 ether, "Attacker should only get its own deposit back");
    }
}

```



---------



## **D4 â€” åˆçº¦é—´è°ƒç”¨**

- **å­¦ä¹ ç‚¹**ï¼š
    - `call`Â /Â `delegatecall`Â /Â `staticcall`
    - `fallback`Â /Â `receive`
- **å®æ“ä»»åŠ¡**ï¼š
    - ç¼–å†™ä¸¤ä¸ªåˆçº¦Â `Sender`Â /Â `Receiver`ï¼Œé€šè¿‡Â `call`Â è°ƒç”¨å¯¹æ–¹çš„æ–¹æ³•
- **æµ‹è¯•**ï¼š
    - æ¨¡æ‹Ÿå‘é€ ETHï¼›éªŒè¯äº‹ä»¶æ˜¯å¦è§¦å‘
- **è¾“å‡º**ï¼šJS æµ‹è¯•è„šæœ¬æ‰§è¡Œç»“æœæˆªå›¾

---

### ç¤ºä¾‹ä»£ç 
Receiver.sol
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.10;

/**
 * ç¼–å†™ä¸¤ä¸ªåˆçº¦`Sender`/`Receiver`ï¼Œé€šè¿‡`call`è°ƒç”¨å¯¹æ–¹çš„æ–¹æ³•
 */
contract Receiver {

    // å®šä¹‰ä¸€ä¸ªæ¥æ”¶äº‹ä»¶
    event Received(address indexed from, uint256 indexed amount, string message);

    // æ™®é€šå‡½æ•°ï¼Œå¯è¢« call è°ƒç”¨
    function foo(string calldata _message) external payable returns(uint256 ){
        emit Received(msg.sender, msg.value,_message);
        return 111;
    }

    // ç‰¹æ®Šå‡½æ•° receive
    // ç”¨äºç›´æ¥æ¥æ”¶ ETHï¼ˆæ²¡æœ‰dataä¸”æœ‰receiveæ—¶æ‰§è¡Œï¼‰
    receive() external payable {
        emit Received(msg.sender, msg.value,"Receive was called");
    }

    // ç‰¹æ®Šå‡½æ•° fallback
    // æ¥æ”¶ ETHï¼Œæ²¡æœ‰å‡½æ•°åŒ¹é…ï¼Œä¼šè°ƒç”¨
    fallback() external payable {
        emit Received(msg.sender, msg.value,"Fallback was called");
    }
}
```
Sender.sol
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.10;

contract Sender {

    // è°ƒç”¨ receiver çš„ foo() æ–¹æ³•ï¼ŒåŒæ—¶å¯ä¼  ETH
    // å› ä¸ºå¯¹ _receiver å‘èµ·è½¬è´¦ï¼Œæ‰€ä»¥éœ€è¦æ˜¯ address payable
    // _message ä¸éœ€è¦ä¿®æ”¹ï¼Œæ‰€ä»¥ä½¿ç”¨ calldataï¼Œæ¯” memory å°‘ gas
    function callFoo(address payable _receiver, string calldata _message) external payable {
        (bool success, bytes memory data) = _receiver.call{value: msg.value}(
            // å¯¹ç›®æ ‡å‡½æ•°ç­¾åç¼–ç 
            abi.encodeWithSignature("foo(string)", _message)
        );
        require(success, "call failed");

        // è¿™é‡Œéœ€è¦ç”¨ (uint256)ï¼Œæ²¡æœ‰æ‹¬å·ä¼šç¼–è¯‘å¤±è´¥
        uint256 returnValue = abi.decode(data, (uint256));
        require(returnValue == 111, "Unexpected return value");
    }

    // ç›´æ¥å‘é€ ETHï¼ˆæµ‹è¯• receive/fallbackï¼‰
    function sendETH(address payable _receiver) external payable {
        (bool success,) = _receiver.call{value: msg.value}("");
        require(success, "send ETH failed");
    }
}
```



### æµ‹è¯•ä»£ç 
D4_CallTest.t.sol
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import "./Receiver.sol";
import "./Sender.sol";
import {Test} from "forge-std/Test.sol";

contract D4_CallTest is Test {

    Receiver public receiver;
    Sender public sender;
    address user;

    function setUp() public {
        receiver = new Receiver();
        sender = new Sender();
        user = makeAddr("user");
        vm.deal(user, 100 ether);
    }

    // æµ‹è¯•è°ƒç”¨ foo
    function testCallFooWithETH() public {
        vm.prank(user);
        vm.expectEmit(true, true, false, true);
        emit Receiver.Received(address(sender), 1 ether, "Hello from Sender");

        sender.callFoo{value: 1 ether}(payable(address(receiver)), "Hello from Sender");
    }

    // æµ‹è¯•ç›´æ¥å‘é€ ETH
    function testSendETH() public {
        vm.prank(user);
        vm.expectEmit(true, true, false, true);
        emit Receiver.Received(address(sender), 0.5 ether, "Receive was called");

        sender.sendETH{value: 0.5 ether}(payable(address(receiver)));
    }
}
```

--------



## **D5 â€” DAO æŠ•ç¥¨åˆçº¦ & **D6 â€” Foundry æµ‹è¯•æ”¹é€ 

- **å­¦ä¹ ç‚¹**ï¼š
    - `struct`Â ç®¡ç†ææ¡ˆ
    - `mapping`Â å­˜å‚¨æŠ•ç¥¨è®°å½•
    - æ—¶é—´æˆ³æ§åˆ¶å¼€å§‹/ç»“æŸ
- **å®æ“ä»»åŠ¡**ï¼š
    - `createProposal(string desc)`Â åˆ›å»ºææ¡ˆ
    - `vote(uint proposalId)`Â æŠ•ç¥¨
    - `closeProposal(uint proposalId)`Â å…³é—­ & è®¡ç®—ç»“æœ
- **æµ‹è¯•**ï¼š
    - Hardhat æµ‹è¯•ï¼šå¤šäººæŠ•ç¥¨ & æ—¶é—´è¿‡æœŸä¸å¯æŠ•
- **è¾“å‡º**ï¼šæŠ•ç¥¨é€»è¾‘æµ‹è¯•é€šè¿‡æˆªå›¾

--------



- **å­¦ä¹ ç‚¹**ï¼š
  - FoundryÂ `.t.sol`Â æµ‹è¯•å†™æ³•
  - `setUp()`Â åˆå§‹åŒ–
  - äº‹ä»¶æ–­è¨€ã€fuzz æµ‹è¯•
- **å®æ“ä»»åŠ¡**ï¼š
  - ç»™ DAO æŠ•ç¥¨åˆçº¦åŠ Â `.t.sol`Â æµ‹è¯•
- **è¾“å‡º**ï¼šFoundry æµ‹è¯•è¦†ç›–ç‡æŠ¥å‘Š



---

### ç¤ºä¾‹ä»£ç 

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.10;

/*
    - `createProposal(string desc)`åˆ›å»ºææ¡ˆ
    - `vote(uint proposalId)`æŠ•ç¥¨
    - `closeProposal(uint proposalId)`å…³é—­ & è®¡ç®—ç»“æœ
 */
contract DaoVoting {

    struct Proposal {
        string description;             // ææ¡ˆæè¿°
        uint256 voteCount;              // æŠ•ç¥¨æ•°
        uint256 startTime;              // å¼€å§‹æ—¶é—´
        uint256 endTime;                // ç»“æŸæ—¶é—´
        bool closed;                    // ç»“æŸæ ‡è®°
        mapping(address => bool) voted; // æŠ•ç¥¨äºº
    }

    Proposal[] public proposals;        // æ‰€æœ‰ææ¡ˆ

    event ProposalCreated(uint256 proposalId, string description, uint256 startTime, uint256 endTime);
    event Voted(uint256 proposalId, address voter);
    event ProposalClosed(uint256 proposalId, uint256 finalVoteCount);

    // åˆ›å»ºææ¡ˆ
    /*
        æ—¶é—´å•ä½ï¼šï¼ˆéœ€è¦æ³¨æ„æ¶¦ç§’ï¼‰
            1 seconds
            1 minutes
            1 hours
            1 days
            1 weeks
     */
    function createProposal(string calldata desc) external {
        uint256 start = block.timestamp;
        uint256 end = block.timestamp + 3 days;

        // åˆ›å»º Proposal ç»“æ„ä½“å¹¶æ¨å…¥æ•°ç»„ï¼ˆè¿™æ ·æè¿°å¯èƒ½ä¸å¤ªåˆé€‚ï¼‰
        // å’Œ Java ä¸åŒï¼Œä¸èƒ½ä½¿ç”¨ new å»åˆ›å»º structã€‚
        // .push() æ˜¯ storage åŠ¨æ€æ•°ç»„çš„æ ‡å‡†åˆå§‹åŒ–æ–¹å¼ï¼Œ
        proposals.push();
        Proposal storage p = proposals[proposals.length - 1];
        p.description = desc;
        p.voteCount = 0;
        p.startTime = start;
        p.endTime = end;
        p.closed = false;

        emit ProposalCreated(proposals.length - 1, desc, start, end);
    }

    // æŠ•ç¥¨ï¼Œå¯¹ä¸€ä¸ªææ¡ˆè¿›è¡ŒæŠ•ç¥¨ï¼ŒæŠ•äº†å°±æ˜¯åŒæ„
    function vote(uint proposalId) external {
        // æ ¡éªŒè®®é¢˜
        require(proposalId < proposals.length, "Proposal not found");
        // ä½¿ç”¨ storageï¼Œè¡¨ç¤ºè·å– storage ä¸­çš„å¼•ç”¨ï¼›memory çš„è¯ï¼Œä¼šåˆ›å»ºä¸€ä¸ªå†…å­˜å‰¯æœ¬
        Proposal storage p = proposals[proposalId];
        // æ ¡éªŒæ—¶é—´
        require(block.timestamp >= p.startTime, "Voting not started");
        require(block.timestamp <= p.endTime, "Voting ended");
        // æ ¡éªŒæŠ•ç¥¨äººï¼Œp.voted[msg.sender] ä¸å­˜åœ¨ï¼Œé»˜è®¤å€¼æ˜¯ false
        require(!p.voted[msg.sender], "Already voted");

        p.voted[msg.sender] = true;
        p.voteCount += 1;

        emit Voted(proposalId, msg.sender);
    }

    // å…³é—­ & è®¡ç®—ç»“æœï¼Œå…³é—­ææ¡ˆå¹¶è¾“å‡ºæœ€ç»ˆç¥¨æ•°
    function closeProposal(uint proposalId) external {
        require(proposalId < proposals.length, "Proposal not found");
        Proposal storage p = proposals[proposalId];
        require(block.timestamp > p.endTime, "Voting still active");
        require(!p.closed, "Already closed");

        p.closed = true;
        emit ProposalClosed(proposalId, p.voteCount);
    }

    function getProposal(uint proposalId) external view returns (
        string memory description,
        uint256 voteCount,
        uint256 startTime,
        uint256 endTime,
        bool closed
    ){
        require(proposalId < proposals.length, "Proposal not found");
        Proposal storage p = proposals[proposalId];
        return (p.description, p.voteCount, p.startTime, p.endTime, p.closed);
    }
}

```

### æµ‹è¯•ä»£ç 

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.20;

import "./DaoVoting.sol";
import {Test} from "forge-std/Test.sol";

contract DaoVotingTest is Test {

    DaoVoting dao;
    address alice;
    address bob;
    address carol;

    function setUp() public {
        dao = new DaoVoting();
        alice = makeAddr("alice");
        bob = makeAddr("bob");
        carol = makeAddr("carol");
    }

    function testCreateProposal() public {
        dao.createProposal("Hello DAO");

        (
            string memory description,
            uint256 voteCount,
            uint256 startTime,
            uint256 endTime,
            bool closed
        ) = dao.getProposal(0);

        assertEq(description, "Hello DAO");
        assertEq(voteCount, 0);
        assertFalse(closed);
        assertTrue(startTime < endTime);
    }

    function testVoteAndCount() public {
        dao.createProposal("Vote Test");

        vm.startPrank(alice);
        dao.vote(0);

        vm.startPrank(bob);
        dao.vote(0);

        vm.startPrank(carol);
        dao.vote(0);

        (, uint256 voteCount,,,) = dao.getProposal(0);

        assertEq(voteCount, 3);
    }

    function testCannotDoubleVote() public {
        dao.createProposal("No Double Voting");

        vm.startPrank(alice);
        dao.vote(0);

        vm.expectRevert(bytes("Already voted"));
        dao.vote(0);
    }

    function testCannotVoteAfterEndTime() public {
        dao.createProposal("Time Limit Test");

        (, , , uint256 endTime, ) = dao.getProposal(0);
        // Sets `block.timestamp`. è·³æ—¶é—´ï¼šè¶…è¿‡ç»“æŸæ—¶é—´
        vm.warp(endTime + 1);

        vm.expectRevert(bytes("Voting ended"));
        vm.prank(alice);
        dao.vote(0);
    }

    function testCloseProposal() public {
        dao.createProposal("Close Test");

        (, , , uint256 endTime, ) = dao.getProposal(0);
        vm.warp(endTime + 1);

        vm.expectEmit(true, false, false, true);
        emit DaoVoting.ProposalClosed(0, 0);
        dao.closeProposal(0);

        (, , , , bool closed) = dao.getProposal(0);
        assertTrue(closed);
    }
}
```



## **D7 â€” æœ¬å‘¨å¤ç›˜**

- æ¸…ç†æœ¬å‘¨ä»£ç åˆ°Â `week1/`Â ç›®å½•
- æ•´ç†æœ¬å‘¨ç¬”è®°ï¼ˆæ¨¡å¼ã€é—®é¢˜ã€ä¼˜åŒ–ï¼‰
- ä¸Šä¼  GitHub
